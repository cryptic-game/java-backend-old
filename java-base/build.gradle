plugins {
    id 'java-library'
}

dependencies {
    /* Logger */
    api('org.slf4j:slf4j-api:1.7.30')
    implementation('ch.qos.logback:logback-classic:1.2.3')
    implementation('io.sentry:sentry-logback:1.7.30')

    /* Bootstrapping & Dependency Injection */
    api('org.springframework:spring-context:5.2.9.RELEASE')
    annotationProcessor("org.springframework:spring-context-indexer:5.2.9.RELEASE")
    api('jakarta.annotation:jakarta.annotation-api:1.3.5')

    /* Database */
    api('jakarta.persistence:jakarta.persistence-api:2.2.3')
    api('jakarta.transaction:jakarta.transaction-api:1.3.3')
    api('org.springframework.data:spring-data-jpa:2.3.4.RELEASE')
    implementation('com.zaxxer:HikariCP:3.4.5')
    api('org.hibernate:hibernate-core:5.4.22.Final') {
        exclude group: "javax.activation", module: "javax.activation-api"
        exclude group: "javax.persistence", module: "javax.persistence-api"
        exclude group: "org.jboss.spec.javax.transaction", module: "jboss-transaction-api_1.2_spec"
    }

    runtimeOnly('org.postgresql:postgresql:42.2.18')
    runtimeOnly('org.mariadb.jdbc:mariadb-java-client:2.7.0')
    runtimeOnly('com.h2database:h2:1.4.200')

    /* Network */
    api(platform('io.projectreactor:reactor-bom:Dysprosium-SR12'))
    api('io.projectreactor:reactor-core')
    api('io.projectreactor.netty:reactor-netty')
    api('io.projectreactor.addons:reactor-extra')

    /* Json */
    api('com.google.code.gson:gson:2.8.6')

    /* Spring Security */
    api('org.springframework.security:spring-security-core:5.4.1')
    api('org.bouncycastle:bcpkix-jdk15on:1.66')

    /* JWT */
    api('io.jsonwebtoken:jjwt-api:0.11.2')
    runtimeOnly('io.jsonwebtoken:jjwt-impl:0.11.2')
    runtimeOnly('io.jsonwebtoken:jjwt-gson:0.11.2')

    /* Annotations */
    compileOnlyApi('org.jetbrains:annotations:20.1.0')

    /* Metrics */
    api('io.micrometer:micrometer-core:1.5.5')
    implementation('io.micrometer:micrometer-registry-influx:1.5.5')

    /* Caching */
    runtimeOnly('io.lettuce:lettuce-core:5.3.4.RELEASE')
    api('org.springframework.data:spring-data-redis:2.3.4.RELEASE')
}
