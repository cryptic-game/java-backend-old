buildscript {
    ext {
        novaVersion = 'edge'
        freefairVersion = '5.3.3.3'
        springBootVersion = '2.5.0'
        springDocVersion = '1.5.9'
    }
}

plugins {
    id 'base'
    id 'io.freefair.git-version' version "${freefairVersion}"
    id 'io.freefair.aggregate-javadoc' version "${freefairVersion}"
    id 'io.freefair.javadoc-utf-8' version "${freefairVersion}"
    id 'io.freefair.lombok' version "${freefairVersion}" apply(false)
    id 'com.github.spotbugs' version '4.7.1' apply(false)
    id 'org.springframework.boot' version "${springBootVersion}" apply false
    id 'io.spring.dependency-management' version '1.0.11.RELEASE' apply false
    id 'org.springframework.experimental.aot' version '0.10.0-SNAPSHOT'
}

if (version.toString().endsWith('SNAPSHOT') || version.toString().isBlank()) {
    version = 'edge'
} else if (version.toString().startsWith('v')) {
    version = version.toString().substring(1)
}

allprojects {
    group 'net.cryptic-game'
}

subprojects {
    version rootProject.version

    apply from: "${rootDir}/gradle/java.gradle"
    apply from: "${rootDir}/gradle/test.gradle"
    apply from: "${rootDir}/gradle/checkstyle.gradle"
    apply from: "${rootDir}/gradle/spotbugs.gradle"
    apply from: "${rootDir}/gradle/pmd.gradle"
    apply from: "${rootDir}/gradle/javadoc.gradle"
}

task copyJavaDocStyles(type: Copy) {
    from "$rootDir/gradle/javadoc.css"
    rename 'javadoc.css', 'stylesheet.css'
    into "$buildDir/docs/aggregateJavadoc/"
}

aggregateJavadoc {
    finalizedBy copyJavaDocStyles
}
