apply plugin: 'java'
apply plugin: 'io.freefair.lombok'

repositories {
    mavenCentral {
        content { excludeGroup('net.getnova.framework') }
    }
    maven {
        url 'https://repo.spring.io/snapshot'
        content { excludeGroup('net.getnova.framework') }
    }
    maven {
        url 'https://raw.githubusercontent.com/getnova/maven/main'
        content { includeGroup('net.getnova.framework') }
    }
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

dependencies {
}

java {
    withJavadocJar()
    withSourcesJar()
}

tasks.withType(Jar) {
    from "${rootDir}/LICENSE"
    manifest.attributes([
            "Implementation-Title"    : 'Cryptic ' + project.name.split('-').collect { it.capitalize() }.join(' '),
            "Implementation-Version"  : project.version,
            "Implementation-Vendor"   : "Cryptic Game (https://cryptic-game.net)",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
    ])
}

lombok {
    config.put('lombok.equalsandhashcode.callsuper', 'CALL')
}
